repositories {
  mavenCentral()
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'checkstyle'
apply plugin: 'jdepend'
apply plugin: 'application'

version = '1.0.0'
project.mainClassName = "org.copinf.cc.Main"
targetCompatibility = 1.8

jar {
  manifest {
    attributes 'Main-Class': project.mainClassName
  }
}

checkstyle {
  configFile = rootProject.file('.checkstyle.xml')
  toolVersion = '6.18'
}

checkstyleMain {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

findbugs {
  ignoreFailures = true
  effort = "max"
  reportLevel = "low"
}

findbugsMain {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

findbugsTest {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

jdependMain {
  reports {
    xml.enabled = false
    text.enabled = true
  }
}

pmd {
  ignoreFailures = true
  ruleSets = [
    "java-basic", "java-braces", "java-clone", "java-codesize", "java-controversial", "java-design",
    "java-finalizers", "java-imports", "java-junit", "java-logging-java", "java-migrating",
    "java-naming", "java-optimizations", "java-strictexception", "java-strings", "java-sunsecure",
    "java-typeresolution", "java-unusedcode"]
}

configurations {
  umljavadoc
}

dependencies {
  testCompile 'junit:junit:4.12'
  umljavadoc 'org.umlgraph:umlgraph:5.6.6'
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

task javadocUml(dependsOn: classes) {
  setDescription('Generates Javadoc API documentation with UMLGraph diagrams')
  setGroup(JavaBasePlugin.DOCUMENTATION_GROUP)
  doLast {
    ant.javadoc(classpath: "{path_to android jar}",
                sourcepath: file('src/main/java'),
                packagenames: '*',
                destdir: "build/docs/javadocUml",
                private: 'true',
                docletpath: configurations.umljavadoc.asPath,
                charset: 'UTF-8',
                encoding: 'UTF-8',
                docencoding: 'UTF-8') {
      doclet(name: 'org.umlgraph.doclet.UmlGraphDoc') {
        // param(name: '-all')
        param(name: '-link', value: 'https://docs.oracle.com/javase/8/docs/api')
        param(name: '-outputencoding', value: 'UTF-8')
        param(name: '-inferrel')
        param(name: '-inferdep')
        param(name: '-qualify')
        param(name: '-postfixpackage')
        param(name: '-hide', value: 'java.*')
      }
    }
  }
}
